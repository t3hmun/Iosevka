import [add-common-feature add-feature add-lookup] from "./table-util"
extern Set

define MarkClasses {
	'above' 'below' 'overlay' 'slash' 'topright' 'bottomright'
	'trailing' 'lf' 'tieAbove' 'tieBelow' 'aboveBrace' 'belowBrace'
}

export : define [buildMarkMkmk sink glyphStore] : begin
	define mark : add-feature sink 'mark'
	define mkmk : add-feature sink 'mkmk'
	add-common-feature sink mark
	add-common-feature sink mkmk

	local markLookupNames {}
	local mkmkLookupNames {}

	foreach markCls [items-of MarkClasses] : begin
		local [object markSubtable mkmkSubtable] : createMTSubtables glyphStore { markCls }
		if ([objectIsNotEmpty markSubtable.marks] && [objectIsNotEmpty markSubtable.bases]) : begin
			local markLookup : add-lookup sink {.type 'gpos_mark_to_base' .subtables { markSubtable }}
			mark.lookups.push    markLookup
			markLookupNames.push markLookup

		if ([objectIsNotEmpty mkmkSubtable.marks] && [objectIsNotEmpty mkmkSubtable.bases]) : begin
			local mkmkLookup : add-lookup sink {.type 'gpos_mark_to_mark' .subtables { mkmkSubtable }}
			mkmk.lookups.push    mkmkLookup
			mkmkLookupNames.push mkmkLookup

	foreach markLookup [items-of markLookupNames] : foreach mkmkLookup [items-of mkmkLookupNames]
		sink.lookupDep.push { markLookup mkmkLookup }

define [createMTSubtables glyphStore markClasses] : begin
	local markSubtable {.marks {.} .bases {.}}
	local mkmkSubtable {.marks {.} .bases {.}}
	local allowMarkClsSet : new Set markClasses
	foreach { gn glyph } [glyphStore.namedEntries] : begin
		createMarkInfo markSubtable.marks gn glyph allowMarkClsSet
		createMarkInfo mkmkSubtable.marks gn glyph allowMarkClsSet
		local isMark : objectIsNotEmpty glyph.markAnchors
		if isMark
			createBaseInfo mkmkSubtable.bases gn glyph allowMarkClsSet
			createBaseInfo markSubtable.bases gn glyph allowMarkClsSet
	return : object markSubtable mkmkSubtable

define [createBaseInfo sink gn glyph allowMarkClsSet] : begin
	local res {.}
	local pushed false
	foreach { markCls anchor } [pairs-of glyph.baseAnchors] : if [allowMarkClsSet.has markCls] : begin
		set pushed true
		set res.(markCls) {.x anchor.x .y anchor.y}
	if pushed : set sink.(gn) res
	return pushed

define [createMarkInfo sink gn glyph allowMarkClsSet] : begin
	local m null
	foreach { markCls anchor } [pairs-of glyph.markAnchors] : if [allowMarkClsSet.has markCls] : begin
		set m {.class markCls .x anchor.x .y anchor.y}
	if m : set sink.(gn) m
	return m

define [objectIsNotEmpty obj] : obj && [Object.keys obj].length