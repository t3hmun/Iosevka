###### Braille symbols
$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../support/utils'
import [designParameters] from '../../meta/aesthetics'

# Needed for bitwise operators
import '../../support/mask-bit' as maskBit

glyph-module

glyph-block Symbol-Braille : begin
	if recursive : return nothing

	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	local left : Middle - (0.25 * Width)
	local right : Middle + (0.25 * Width)
	local offsetY : 0.3 * (SymbolMid - PictTop)
	local stress : 0.5 * [adviceBlackness 3.375]
	local dotRadius : PeriodRadius * [Math.min 1 (stress / HalfStroke)]

	### Computed Braille Patterns
	###
	### Bit to Dot mapping:
	###   0  3
	###   1  4
	###   2  5
	###   6  7
	###
	foreach [byte : range 1 256] : begin
		local dots {}
		foreach [bit : range 0 8] : if [maskBit byte bit] : dots.push (bit + 1)
		create-glyph ("braille" + [dots.join '']) (0x2800 + byte) : glyph-proc
			set currentGlyph.autoRefPriority (-101)
			local [y-coord y] (SymbolMid + y * offsetY)
			# Is there a way to directly use (byte & (1 << bit)) here?
			foreach [bit : range 0 8] : if [maskBit byte bit] : begin
				include : match bit
					0 : DotAt left  [y-coord (-3)] dotRadius
					1 : DotAt left  [y-coord (-1)] dotRadius
					2 : DotAt left  [y-coord   1 ] dotRadius
					3 : DotAt right [y-coord (-3)] dotRadius
					4 : DotAt right [y-coord (-1)] dotRadius
					5 : DotAt right [y-coord   1 ] dotRadius
					6 : DotAt left  [y-coord   3 ] dotRadius
					7 : DotAt right [y-coord   3 ] dotRadius

	if [query-glyph 'braille7'] : set [query-glyph 'braille7'].autoRefPriority             101
	if [query-glyph 'braille37'] : set [query-glyph 'braille37'].autoRefPriority           102
	if [query-glyph 'braille3'] : set [query-glyph 'braille3'].autoRefPriority             101
	if [query-glyph 'braille237'] : set [query-glyph 'braille237'].autoRefPriority         103
	if [query-glyph 'braille27'] : set [query-glyph 'braille27'].autoRefPriority           102
	if [query-glyph 'braille23'] : set [query-glyph 'braille23'].autoRefPriority           102
	if [query-glyph 'braille2'] : set [query-glyph 'braille2'].autoRefPriority             101
	if [query-glyph 'braille1237'] : set [query-glyph 'braille1237'].autoRefPriority       104
	if [query-glyph 'braille123'] : set [query-glyph 'braille123'].autoRefPriority         103
	if [query-glyph 'braille127'] : set [query-glyph 'braille127'].autoRefPriority         103
	if [query-glyph 'braille137'] : set [query-glyph 'braille137'].autoRefPriority         103
	if [query-glyph 'braille13'] : set [query-glyph 'braille13'].autoRefPriority           102
	if [query-glyph 'braille12'] : set [query-glyph 'braille12'].autoRefPriority           102
	if [query-glyph 'braille17'] : set [query-glyph 'braille17'].autoRefPriority           102
	if [query-glyph 'braille1'] : set [query-glyph 'braille1'].autoRefPriority             101
	if [query-glyph 'braille7'] : set [query-glyph 'braille7'].avoidBeingComposite        true
	if [query-glyph 'braille37'] : set [query-glyph 'braille37'].avoidBeingComposite      true
	if [query-glyph 'braille3'] : set [query-glyph 'braille3'].avoidBeingComposite        true
	if [query-glyph 'braille237'] : set [query-glyph 'braille237'].avoidBeingComposite    true
	if [query-glyph 'braille27'] : set [query-glyph 'braille27'].avoidBeingComposite      true
	if [query-glyph 'braille23'] : set [query-glyph 'braille23'].avoidBeingComposite      true
	if [query-glyph 'braille2'] : set [query-glyph 'braille2'].avoidBeingComposite        true
	if [query-glyph 'braille1237'] : set [query-glyph 'braille1237'].avoidBeingComposite  true
	if [query-glyph 'braille123'] : set [query-glyph 'braille123'].avoidBeingComposite    true
	if [query-glyph 'braille127'] : set [query-glyph 'braille127'].avoidBeingComposite    true
	if [query-glyph 'braille137'] : set [query-glyph 'braille137'].avoidBeingComposite    true
	if [query-glyph 'braille13'] : set [query-glyph 'braille13'].avoidBeingComposite      true
	if [query-glyph 'braille12'] : set [query-glyph 'braille12'].avoidBeingComposite      true
	if [query-glyph 'braille17'] : set [query-glyph 'braille17'].avoidBeingComposite      true
	if [query-glyph 'braille1'] : set [query-glyph 'braille1'].avoidBeingComposite        true
