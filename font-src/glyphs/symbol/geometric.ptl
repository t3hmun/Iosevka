$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../support/utils'
import [designParameters] from '../../meta/aesthetics'

glyph-module

glyph-block Symbol-Geometric : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	### Standard geometric shapes
	for-width-kinds WideWidth1
		define leftFW : 0 + SB * MosaicWidthScalar
		define rightFW : MosaicWidth - SB * MosaicWidthScalar
		define squareRadiusFW : (rightFW - leftFW) / 2
		define topFW : SymbolMid + squareRadiusFW
		define bottomFW : SymbolMid - squareRadiusFW

		define scaffold : DivFrame MosaicWidthScalar

		define [hollowScale w s] : (w - [fallback s GeometryStroke]) / w

		define [hollow newid unicode oldid zx zy cx cy] : create-glyph newid unicode : glyph-proc
			set-width MosaicWidth
			currentGlyph.dependsOn : query-glyph oldid
			local zoom : hollowScale squareRadiusFW
			include : difference
				new-glyph : glyph-proc
					include : refer-glyph oldid
				new-glyph : glyph-proc
					include : refer-glyph oldid
					include : FlipAround [fallback cx MosaicMiddle] [fallback cy SymbolMid] [fallback zx zoom] [fallback zy zx zoom]

		define [hollowWithCenter newid unicode oldid zx zy cx cy] : create-glyph newid unicode : glyph-proc
			set-width MosaicWidth
			currentGlyph.dependsOn : query-glyph oldid
			local zoom : hollowScale squareRadiusFW
			local zx1 : mix [fallback zx zoom] 1 0.25
			local zx2 : mix zx1 0 0.5
			local zy1 : mix [fallback zy zx zoom] 1 0.25
			local zy2 : mix zy1 0 0.5
			include : union
				difference
					new-glyph : glyph-proc
						include : refer-glyph oldid
					new-glyph : glyph-proc
						include : refer-glyph oldid
						include : FlipAround [fallback cx MosaicMiddle] [fallback cy SymbolMid] zx1 zy1
				new-glyph : glyph-proc
					include : refer-glyph oldid
					include : FlipAround [fallback cx MosaicMiddle] [fallback cy SymbolMid] zx2 zy2

		define [doubleHollow newid unicode oldid zx zy cx cy] : create-glyph newid unicode : glyph-proc
			set-width MosaicWidth
			currentGlyph.dependsOn : query-glyph oldid
			local zoom : hollowScale squareRadiusFW
			local zx1 : mix [fallback zx zoom] 1 0.25
			local zx2 : [mix zx1 0 0.5] + (1 - zx1) / 2
			local zx3 : zx2 - (1 - zx1)
			local zy1 : mix [fallback zy zx zoom] 1 0.25
			local zy2 : [mix zy1 0 0.5] + (1 - zy1) / 2
			local zy3 : zy2 - (1 - zy1)
			include : union
				difference
					new-glyph : glyph-proc
						include : refer-glyph oldid
					new-glyph : glyph-proc
						include : refer-glyph oldid
						include : FlipAround [fallback cx MosaicMiddle] [fallback cy SymbolMid] zx1 zy1
				difference
					new-glyph : glyph-proc
						include : refer-glyph oldid
						include : FlipAround [fallback cx MosaicMiddle] [fallback cy SymbolMid] zx2 zy2
					new-glyph : glyph-proc
						include : refer-glyph oldid
						include : FlipAround [fallback cx MosaicMiddle] [fallback cy SymbolMid] zx3 zy3

		define [resized newid unicode id x y s sy] : create-glyph newid unicode : glyph-proc
			set-width MosaicWidth
			include [refer-glyph id] AS_BASE
			include : FlipAround x y s [fallback sy s]

		define [UpperHalfMask] : Rect (SymbolMid + squareRadiusFW * 2) SymbolMid 0 MosaicWidth
		define [LowerHalfMask] : Rect SymbolMid (SymbolMid - squareRadiusFW * 2) 0 MosaicWidth
		define [LeftHalfMask]  : Rect (SymbolMid + squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2) 0 MosaicMiddle
		define [RightHalfMask] : Rect (SymbolMid + squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2) MosaicMiddle MosaicWidth
		define [UpperLeftQuarterMark]  : intersection [UpperHalfMask] [LeftHalfMask]
		define [UpperRightQuarterMark] : intersection [UpperHalfMask] [RightHalfMask]
		define [LowerLeftQuarterMark]  : intersection [LowerHalfMask] [LeftHalfMask]
		define [LowerRightQuarterMark] : intersection [LowerHalfMask] [RightHalfMask]
		define [UpperLeftHalfMark]  : spiro-outline
			corner  (MosaicMiddle + squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)
			corner  (MosaicMiddle - squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)
			corner  (MosaicMiddle - squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)
		define [LowerRightHalfMark] : spiro-outline
			corner  (MosaicMiddle + squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)
			corner  (MosaicMiddle - squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)
			corner  (MosaicMiddle + squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)
		define [UpperRightHalfMark]  : spiro-outline
			corner  (MosaicMiddle + squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)
			corner  (MosaicMiddle - squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)
			corner  (MosaicMiddle + squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)
		define [LowerLeftHalfMark] : spiro-outline
			corner  (MosaicMiddle - squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)
			corner  (MosaicMiddle - squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)
			corner  (MosaicMiddle + squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)

		create-glyph [MangleName 'blackSquare'] [MangleUnicode 0x25A0] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW (SymbolMid + squareRadiusFW)
				corner rightFW (SymbolMid + squareRadiusFW)
				corner rightFW (SymbolMid - squareRadiusFW)
				corner leftFW (SymbolMid - squareRadiusFW)

		create-glyph [MangleName 'smallBlackSquare'] [MangleUnicode 0x25AA] : glyph-proc
			set-width MosaicWidth
			local r : squareRadiusFW * designParameters.geometric_small_x
			include : spiro-outline
				corner (MosaicMiddle - r) (SymbolMid + r)
				corner (MosaicMiddle + r) (SymbolMid + r)
				corner (MosaicMiddle + r) (SymbolMid - r)
				corner (MosaicMiddle - r) (SymbolMid - r)

		create-glyph [MangleName 'extraSmallBlackSquare'] [MangleUnicode 0x25FE] : glyph-proc
			set-width MosaicWidth
			local r : squareRadiusFW * designParameters.geometric_extra_small_x
			include : spiro-outline
				corner (MosaicMiddle - r) (SymbolMid + r)
				corner (MosaicMiddle + r) (SymbolMid + r)
				corner (MosaicMiddle + r) (SymbolMid - r)
				corner (MosaicMiddle - r) (SymbolMid - r)

		create-glyph [MangleName 'mediumBlackSquare'] [MangleUnicode 0x25FC] : glyph-proc
			set-width MosaicWidth
			local r : squareRadiusFW * designParameters.geometric_medium_x
			include : spiro-outline
				corner (MosaicMiddle - r) (SymbolMid + r)
				corner (MosaicMiddle + r) (SymbolMid + r)
				corner (MosaicMiddle + r) (SymbolMid - r)
				corner (MosaicMiddle - r) (SymbolMid - r)

		create-glyph [MangleName 'blackTriangleRB'] [MangleUnicode 0x25E2] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner rightFW (SymbolMid + squareRadiusFW)
				corner rightFW (SymbolMid - squareRadiusFW)
				corner leftFW (SymbolMid - squareRadiusFW)

		create-glyph [MangleName 'blackTriangleLB'] [MangleUnicode 0x25E3] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW (SymbolMid + squareRadiusFW)
				corner rightFW (SymbolMid - squareRadiusFW)
				corner leftFW (SymbolMid - squareRadiusFW)

		create-glyph [MangleName 'blackTriangleLT'] [MangleUnicode 0x25E4] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW (SymbolMid + squareRadiusFW)
				corner rightFW (SymbolMid + squareRadiusFW)
				corner leftFW (SymbolMid - squareRadiusFW)

		create-glyph [MangleName 'blackTriangleRT'] [MangleUnicode 0x25E5] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW (SymbolMid + squareRadiusFW)
				corner rightFW (SymbolMid + squareRadiusFW)
				corner rightFW (SymbolMid - squareRadiusFW)

		create-glyph [MangleName 'blackRectangle'] [MangleUnicode 0x25AC] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW (SymbolMid + squareRadiusFW / 1.5)
				corner rightFW (SymbolMid + squareRadiusFW / 1.5)
				corner rightFW (SymbolMid - squareRadiusFW / 1.5)
				corner leftFW (SymbolMid - squareRadiusFW / 1.5)

		create-glyph [MangleName 'blackParallelogram'] [MangleUnicode 0x25B0] : glyph-proc
			set-width MosaicWidth
			define skew : squareRadiusFW * 1 / 4
			include : spiro-outline
				corner (leftFW + skew) (SymbolMid + squareRadiusFW / 1.5)
				corner (rightFW + skew) (SymbolMid + squareRadiusFW / 1.5)
				corner (rightFW - skew) (SymbolMid - squareRadiusFW / 1.5)
				corner (leftFW - skew) (SymbolMid - squareRadiusFW / 1.5)

		create-glyph [MangleName 'whiteParallelogram'] [MangleUnicode 0x25B1] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'blackParallelogram'
				glyph-proc
					define skew : squareRadiusFW * 1 / 4
					include : dispiro
						widths.rhs GeometryStroke
						flat (leftFW + skew) (SymbolMid + squareRadiusFW / 1.5)
						curl (rightFW + skew) (SymbolMid + squareRadiusFW / 1.5)
					include : dispiro
						widths.rhs GeometryStroke
						flat (rightFW + skew) (SymbolMid + squareRadiusFW / 1.5)
						curl (rightFW - skew) (SymbolMid - squareRadiusFW / 1.5)
					include : dispiro
						widths.rhs GeometryStroke
						flat (rightFW - skew) (SymbolMid - squareRadiusFW / 1.5)
						curl (leftFW - skew) (SymbolMid - squareRadiusFW / 1.5)
					include : dispiro
						widths.rhs GeometryStroke
						flat (leftFW - skew) (SymbolMid - squareRadiusFW / 1.5)
						curl (leftFW + skew) (SymbolMid + squareRadiusFW / 1.5)

		create-glyph [MangleName 'blackLongRectangle'] [MangleUnicode 0x25AE] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW (SymbolMid + squareRadiusFW * 1.5)
				corner rightFW (SymbolMid + squareRadiusFW * 1.5)
				corner rightFW (SymbolMid - squareRadiusFW * 1.5)
				corner leftFW (SymbolMid - squareRadiusFW * 1.5)


		define triangleOvershoot : 2 / [Math.sqrt 3]
		create-glyph [MangleName 'blackTriangleUp'] [MangleUnicode 0x25B2] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner MosaicMiddle (SymbolMid + squareRadiusFW)
				corner [mix MosaicMiddle rightFW triangleOvershoot] (SymbolMid - squareRadiusFW)
				corner [mix MosaicMiddle leftFW triangleOvershoot] (SymbolMid - squareRadiusFW)

		create-glyph [MangleName 'triangularWedge'] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph [MangleName 'blackTriangleUp']
				union
					dispiro
						widths.rhs GeometryStroke
						corner MosaicMiddle (SymbolMid + squareRadiusFW)
						corner [mix MosaicMiddle rightFW triangleOvershoot] (SymbolMid - squareRadiusFW)
					dispiro
						widths.lhs GeometryStroke
						corner MosaicMiddle (SymbolMid + squareRadiusFW)
						corner [mix MosaicMiddle leftFW triangleOvershoot] (SymbolMid - squareRadiusFW)

		create-glyph [MangleName 'blackTriangleDown'] [MangleUnicode 0x25BC] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner MosaicMiddle (SymbolMid - squareRadiusFW)
				corner [mix MosaicMiddle leftFW triangleOvershoot] (SymbolMid + squareRadiusFW)
				corner [mix MosaicMiddle rightFW triangleOvershoot] (SymbolMid + squareRadiusFW)

		create-glyph [MangleName 'triangularVee'] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph [MangleName 'blackTriangleDown']
				union
					dispiro
						widths.lhs GeometryStroke
						corner MosaicMiddle (SymbolMid - squareRadiusFW)
						corner [mix MosaicMiddle rightFW triangleOvershoot] (SymbolMid + squareRadiusFW)
					dispiro
						widths.rhs GeometryStroke
						corner MosaicMiddle (SymbolMid - squareRadiusFW)
						corner [mix MosaicMiddle leftFW triangleOvershoot] (SymbolMid + squareRadiusFW)

		create-glyph [MangleName 'blackTriangleLeft'] [MangleUnicode 0x25C0] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW SymbolMid
				corner rightFW (SymbolMid - squareRadiusFW * triangleOvershoot)
				corner rightFW (SymbolMid + squareRadiusFW * triangleOvershoot)

		create-glyph [MangleName 'blackTriangleRight'] [MangleUnicode 0x25B6] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner rightFW SymbolMid
				corner leftFW (SymbolMid + squareRadiusFW * triangleOvershoot)
				corner leftFW (SymbolMid - squareRadiusFW * triangleOvershoot)

		create-glyph [MangleName 'thinBlackTriangleLeft'] [MangleUnicode 0x25C4] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner leftFW SymbolMid
				corner rightFW (SymbolMid - squareRadiusFW * triangleOvershoot * 0.618)
				corner rightFW (SymbolMid + squareRadiusFW * triangleOvershoot * 0.618)

		create-glyph [MangleName 'thinWhiteTriangleLeft'] [MangleUnicode 0x25C5] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'thinBlackTriangleLeft'
				union
					dispiro
						widths.lhs GeometryStroke
						corner leftFW SymbolMid
						corner rightFW (SymbolMid - squareRadiusFW * triangleOvershoot * 0.618)
					dispiro
						widths.lhs GeometryStroke
						corner rightFW (SymbolMid - squareRadiusFW * triangleOvershoot * 0.618)
						corner rightFW (SymbolMid + squareRadiusFW * triangleOvershoot * 0.618)
					dispiro
						widths.lhs GeometryStroke
						corner rightFW (SymbolMid + squareRadiusFW * triangleOvershoot * 0.618)
						corner leftFW SymbolMid

		create-glyph [MangleName 'thinBlackTriangleRight'] [MangleUnicode 0x25BA] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner rightFW SymbolMid
				corner leftFW (SymbolMid + squareRadiusFW * triangleOvershoot * 0.618)
				corner leftFW (SymbolMid - squareRadiusFW * triangleOvershoot * 0.618)

		create-glyph [MangleName 'thinWhiteTriangleRight'] [MangleUnicode 0x25BB] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'thinBlackTriangleRight'
				union
					dispiro
						widths.rhs GeometryStroke
						corner rightFW SymbolMid
						corner leftFW (SymbolMid - squareRadiusFW * triangleOvershoot * 0.618)
					dispiro
						widths.rhs GeometryStroke
						corner leftFW (SymbolMid - squareRadiusFW * triangleOvershoot * 0.618)
						corner leftFW (SymbolMid + squareRadiusFW * triangleOvershoot * 0.618)
					dispiro
						widths.rhs GeometryStroke
						corner leftFW (SymbolMid + squareRadiusFW * triangleOvershoot * 0.618)
						corner rightFW SymbolMid

		create-glyph [MangleName 'blackDiamond'] [MangleUnicode 0x25C6] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner MosaicMiddle (SymbolMid + squareRadiusFW)
				corner rightFW SymbolMid
				corner MosaicMiddle (SymbolMid - squareRadiusFW)
				corner leftFW SymbolMid

		create-glyph [MangleName 'blackLonzenge'] [MangleUnicode 0x29EB] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				corner MosaicMiddle (SymbolMid + squareRadiusFW)
				corner [mix MosaicMiddle rightFW (2 / 3)] SymbolMid
				corner MosaicMiddle (SymbolMid - squareRadiusFW)
				corner [mix MosaicMiddle leftFW (2 / 3)] SymbolMid

		create-glyph [MangleName 'whiteLonzenge'] [MangleUnicode 0x25CA] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'blackLonzenge'
				new-glyph : glyph-proc
					include : dispiro
						widths.rhs GeometryStroke
						flat MosaicMiddle (SymbolMid + squareRadiusFW)
						curl [mix MosaicMiddle rightFW (2 / 3)] SymbolMid
					include : dispiro
						widths.rhs GeometryStroke
						flat [mix MosaicMiddle rightFW (2 / 3)] SymbolMid
						curl MosaicMiddle (SymbolMid - squareRadiusFW)
					include : dispiro
						widths.rhs GeometryStroke
						flat MosaicMiddle (SymbolMid - squareRadiusFW)
						curl [mix MosaicMiddle leftFW (2 / 3)] SymbolMid
					include : dispiro
						widths.rhs GeometryStroke
						flat [mix MosaicMiddle leftFW (2 / 3)] SymbolMid
						curl MosaicMiddle (SymbolMid + squareRadiusFW)

		create-glyph [MangleName 'blackCircleO'] : glyph-proc
			set-width MosaicWidth
			include : scaffold.markSet.plus
			include : spiro-outline
				g4 MosaicMiddle (SymbolMid + squareRadiusFW + O)
				g4 (leftFW - O) SymbolMid
				g4 MosaicMiddle (SymbolMid - squareRadiusFW - O)
				g4 (rightFW + O) SymbolMid
				close

		create-glyph [MangleName 'blackCircle'] [MangleUnicode 0x25CF] : glyph-proc
			set-width MosaicWidth
			include : scaffold.markSet.plus
			include : spiro-outline
				g4 MosaicMiddle (SymbolMid + squareRadiusFW)
				g4 leftFW SymbolMid
				g4 MosaicMiddle (SymbolMid - squareRadiusFW)
				g4 rightFW SymbolMid
				close

		create-glyph [MangleName 'leftBlackCircle'] [MangleUnicode 0x25D6] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'blackCircle'
				spiro-outline
					corner (0 - MosaicWidth) (SymbolMid - squareRadiusFW * 2)
					corner (MosaicWidth / 2) (SymbolMid - squareRadiusFW * 2)
					corner (MosaicWidth / 2) (SymbolMid + squareRadiusFW * 2)
					corner (0 - MosaicWidth) (SymbolMid + squareRadiusFW * 2)

		create-glyph [MangleName 'rightBlackCircle'] [MangleUnicode 0x25D7] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'blackCircle'
				spiro-outline
					corner (MosaicWidth * 2) (SymbolMid - squareRadiusFW * 2)
					corner (MosaicWidth / 2) (SymbolMid - squareRadiusFW * 2)
					corner (MosaicWidth / 2) (SymbolMid + squareRadiusFW * 2)
					corner (MosaicWidth * 2) (SymbolMid + squareRadiusFW * 2)

		create-glyph [MangleName 'smallBlackCircle'] : glyph-proc
			set-width MosaicWidth
			include : spiro-outline
				g4 MosaicMiddle (SymbolMid + squareRadiusFW * designParameters.geometric_small_x)
				g4 (MosaicMiddle - squareRadiusFW * designParameters.geometric_small_x) SymbolMid
				g4 MosaicMiddle (SymbolMid - squareRadiusFW * designParameters.geometric_small_x)
				g4 (MosaicMiddle + squareRadiusFW * designParameters.geometric_small_x) SymbolMid
				close

		create-glyph [MangleName 'bigBlackCircle'] [MangleUnicode 0x2B24] : glyph-proc
			set-width MosaicWidth
			define scale : designParameters.GeometricLargeX MosaicWidth MosaicUnitWidth
			include : spiro-outline
				g4 MosaicMiddle (SymbolMid + squareRadiusFW * scale)
				g4 (MosaicMiddle - squareRadiusFW * scale) SymbolMid
				g4 MosaicMiddle (SymbolMid - squareRadiusFW * scale)
				g4 (MosaicMiddle + squareRadiusFW * scale) SymbolMid
				close

		let
		: cfg : list
			* {.sides 5 .skip 2 .name 'blackStar' .unicode 0x2605 .overflow 1.1 .phase 0}
			* {.sides 5 .skip 1 .name 'blackPentagon' .unicode 0x2B1F .overflow 1.05 .phase 0}
			* {.sides 6 .skip 1 .name 'blackHexagon' .unicode 0x2B22 .overflow 1.05 .phase 0}
			* {.sides 5 .skip 1 .name 'rhBlackPentagon' .unicode 0x2B53 .overflow 1.05 .phase (-Math.PI / 2)}
			* {.sides 6 .skip 1 .name 'hBlackHexagon' .unicode 0x2B23 .overflow 1.05 .phase (Math.PI / 2)}
		: foreach [[object sides skip name unicode overflow phase] : items-of cfg] : begin
			create-glyph [MangleName name] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				local corners {}
				foreach [j : range 0 sides] : begin
					local angle : 2 * Math.PI * j * skip / sides + phase
					corners.push : corner
						mix leftFW rightFW (0.5 - 0.5 * overflow * [Math.sin angle])
						SymbolMid + squareRadiusFW * [Math.cos angle] * overflow
				include : spiro-outline corners

		let
		: cfg : list
			* {.sides 6 .skip 1 .name 'SixPointedBlackStar' .unicode 0x2736 .overflow 1.05 .phase 0}
		: foreach [[object sides skip name unicode overflow phase] : items-of cfg]
			create-glyph [MangleName name] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				local corners {}
				foreach [j : range 0 (2 * sides)] : begin
					local angle : Math.PI * j * skip / sides + phase
					local mag : [if (j % 2) 0.5 1.0] * overflow
					corners.push : corner
						mix leftFW rightFW (0.5 - 0.5 * mag * [Math.sin angle])
						SymbolMid + squareRadiusFW * mag * [Math.cos angle]
				include : spiro-outline corners

		create-glyph [MangleName 'dottedCircle'] [MangleUnicode 0x25CC] : glyph-proc
			set-width MosaicWidth
			local n : 12 * [Math.ceil MosaicWidthScalar]
			local fine : adviceBlackness 10
			foreach j [range 0 n] : begin
				local angle : Math.PI * 2 * j / n
				local cx : MosaicMiddle + (rightFW - MosaicMiddle) * [Math.cos angle]
				local cy : SymbolMid + (rightFW - MosaicMiddle) * [Math.sin angle]
				include : spiro-outline
					g4 cx (cy + fine)
					g4 (cx + fine) cy
					g4 cx (cy - fine)
					g4 (cx - fine) cy
					close

			include : scaffold.markSet.plus

		hollow [MangleName 'whiteSquare'] [MangleUnicode 0x25A1] [MangleName 'blackSquare']
		hollowWithCenter [MangleName 'whiteSquareWithCenter'] [MangleUnicode 0x25A3] [MangleName 'blackSquare']
		hollow [MangleName 'whiteHexagon'] [MangleUnicode 0x2B21] [MangleName 'blackHexagon']
		hollow [MangleName 'whitePentagon'] [MangleUnicode 0x2B20] [MangleName 'blackPentagon']
		hollow [MangleName 'rhWhitePentagon'] [MangleUnicode 0x2B54] [MangleName 'rhBlackPentagon']
		hollow [MangleName 'whiteRectangle'] [MangleUnicode 0x25AD] [MangleName 'blackRectangle'] [hollowScale squareRadiusFW] [hollowScale (squareRadiusFW / 1.5)]
		hollow [MangleName 'whiteLongRectangle'] [MangleUnicode 0x25AF] [MangleName 'blackLongRectangle'] [hollowScale squareRadiusFW] [hollowScale (squareRadiusFW * 1.5)]

		hollow [MangleName 'whiteCircle'] [MangleUnicode 0x25CB] [MangleName 'blackCircle']
		hollowWithCenter [MangleName 'whiteCircleWithCenter'] [MangleUnicode 0x25C9] [MangleName 'blackCircle']
		doubleHollow [MangleName 'doubleWhiteCircle'] [MangleUnicode 0x25CE] [MangleName 'blackCircle']
		hollow [MangleName 'bigWhiteCircle'] [MangleUnicode 0x25EF] [MangleName 'bigBlackCircle'] [hollowScale : squareRadiusFW * [designParameters.GeometricLargeX MosaicWidth MosaicUnitWidth]]
		hollow [MangleName 'smallWhiteCircle'] null [MangleName 'smallBlackCircle'] [hollowScale : squareRadiusFW * designParameters.geometric_small_x]
		hollow [MangleName 'mediumWhiteSquare'] [MangleUnicode 0x25FB] [MangleName 'mediumBlackSquare'] [hollowScale : squareRadiusFW * designParameters.geometric_medium_x]
		hollow [MangleName 'smallWhiteSquare'] [MangleUnicode 0x25AB] [MangleName 'smallBlackSquare'] [hollowScale : squareRadiusFW * designParameters.geometric_small_x]
		hollow [MangleName 'extraSmallWhiteSquare'] [MangleUnicode 0x25FD] [MangleName 'extraSmallBlackSquare'] [hollowScale [squareRadiusFW * designParameters.geometric_extra_small_x] [adviceBlackness 6 MosaicWidthScalar]]
		do
			define s : hollowScale squareRadiusFW (GeometryStroke * [Math.sqrt 2])
			hollow [MangleName 'whiteDiamond'] [MangleUnicode 0x25C7] [MangleName 'blackDiamond'] s
			hollowWithCenter [MangleName 'whiteDiamondWithCenter'] [MangleUnicode 0x25C8] [MangleName 'blackDiamond'] s

			resized [MangleName 'smallBlackTriangleUp'] [MangleUnicode 0x25B4] [MangleName 'blackTriangleUp'] MosaicMiddle SymbolMid 0.75
			resized [MangleName 'smallBlackTriangleRight'] [MangleUnicode 0x25B8] [MangleName 'blackTriangleRight'] MosaicMiddle SymbolMid 0.75
			resized [MangleName 'smallBlackTriangleDown'] [MangleUnicode 0x25BE] [MangleName 'blackTriangleDown'] MosaicMiddle SymbolMid 0.75
			resized [MangleName 'smallBlackTriangleLeft'] [MangleUnicode 0x25C2] [MangleName 'blackTriangleLeft'] MosaicMiddle SymbolMid 0.75

			define sts 0.75
			define sl : hollowScale (squareRadiusFW * 2 / [Math.sqrt 3]) (GeometryStroke * [Math.sqrt 3])
			define spt : hollowScale (squareRadiusFW * 2 / [Math.sqrt 5]) ([adviceBlackness 5 scaffold.div] * [Math.sqrt 5])
			define slt : hollowScale (squareRadiusFW * 2 / [Math.sqrt 3]) (GeometryStroke * [Math.sqrt 3])
			define ss : hollowScale (squareRadiusFW * sts * 2 / [Math.sqrt 3]) (GeometryStroke * [Math.sqrt 3])

			hollow [MangleName 'whiteTriangleUp'] [MangleUnicode 0x25B3] [MangleName 'blackTriangleUp'] sl nothing MosaicMiddle (SymbolMid - squareRadiusFW / 3)
			hollow [MangleName 'whiteTriangleDown'] [MangleUnicode 0x25BD] [MangleName 'blackTriangleDown'] sl nothing MosaicMiddle (SymbolMid + squareRadiusFW / 3)
			hollow [MangleName 'whiteTriangleLeft'] [MangleUnicode 0x25C1] [MangleName 'blackTriangleLeft'] sl nothing [mix MosaicMiddle rightFW (1 / 3)] SymbolMid
			hollow [MangleName 'whiteTriangleRight'] [MangleUnicode 0x25B7] [MangleName 'blackTriangleRight'] sl nothing [mix MosaicMiddle leftFW (1 / 3)] SymbolMid
			hollow [MangleName 'smallWhiteTriangleUp'] [MangleUnicode 0x25B5] [MangleName 'smallBlackTriangleUp'] ss nothing MosaicMiddle (SymbolMid - squareRadiusFW / 3 * sts)
			hollow [MangleName 'smallWhiteTriangleDown'] [MangleUnicode 0x25BF] [MangleName 'smallBlackTriangleDown'] ss nothing MosaicMiddle (SymbolMid + squareRadiusFW / 3 * sts)
			hollow [MangleName 'smallWhiteTriangleLeft'] [MangleUnicode 0x25C3] [MangleName 'smallBlackTriangleLeft'] ss nothing [mix MosaicMiddle rightFW (sts / 3)] SymbolMid
			hollow [MangleName 'smallWhiteTriangleRight'] [MangleUnicode 0x25B9] [MangleName 'smallBlackTriangleRight'] ss nothing [mix MosaicMiddle leftFW (sts / 3)] SymbolMid
			hollow [MangleName 'whiteStar'] [MangleUnicode 0x2606] [MangleName 'blackStar'] spt

		# Dice
		do "Dice"
			define diceCombinations : list
				list 0x2680 { { 1 1 } }
				list 0x2681 { { 0 0 } { 2 2 } }
				list 0x2682 { { 0 0 } { 1 1 } { 2 2 } }
				list 0x2683 { { 0 0 } { 2 0 } { 0 2 } { 2 2 } }
				list 0x2684 { { 0 0 } { 2 0 } { 1 1 } { 0 2 } { 2 2 } }
				list 0x2685 { { 0 0 } { 0 1 } { 0 2 } { 2 0 } { 2 1 } { 2 2 } }

			foreach { unicode dots } [items-of diceCombinations] : begin
				create-glyph [MangleName "dice_\(unicode)"] [MangleUnicode unicode] : glyph-proc
					include [refer-glyph : MangleName 'whiteSquare'] AS_BASE ALSO_METRICS
					define dr : DotRadius / Stroke * [adviceBlackness 7 MosaicWidthScalar]
					define xLeftEx   : MosaicMiddle - squareRadiusFW + GeometryStroke
					define xRightEx  : MosaicMiddle + squareRadiusFW - GeometryStroke
					define yBottomEx : SymbolMid + squareRadiusFW - GeometryStroke
					define yTopEx    : SymbolMid - squareRadiusFW + GeometryStroke
					define xLeft     : mix xLeftEx xRightEx (1 / 5)
					define xRight    : mix xLeftEx xRightEx (4 / 5)
					define yBottom   : mix yBottomEx yTopEx (1 / 5)
					define yTop      : mix yBottomEx yTopEx (4 / 5)
					foreach { px py } [items-of dots] : begin
						include : CircleDotAt
							mix xLeft xRight (px / 2)
							mix yBottom yTop (py / 2)
							begin dr

		# Rounded
		create-glyph [MangleName 'roundwhitesquare'] [MangleUnicode 0x25A2] : glyph-proc
			set-width MosaicWidth
			define cr : squareRadiusFW - GeometryStroke * 2
			include : dispiro
				widths.lhs GeometryStroke
				flat (MosaicMiddle + 1) (SymbolMid + squareRadiusFW) [heading Leftward]
				curl (MosaicMiddle - cr) (SymbolMid + squareRadiusFW) [heading Leftward]
				archv
				flat (MosaicMiddle - squareRadiusFW) (SymbolMid + cr) [heading Downward]
				curl (MosaicMiddle - squareRadiusFW) (SymbolMid - cr) [heading Downward]
				arcvh
				flat (MosaicMiddle - cr) (SymbolMid - squareRadiusFW) [heading Rightward]
				curl (MosaicMiddle + cr) (SymbolMid - squareRadiusFW) [heading Rightward]
				archv
				flat (MosaicMiddle + squareRadiusFW) (SymbolMid - cr) [heading Upward]
				curl (MosaicMiddle + squareRadiusFW) (SymbolMid + cr) [heading Upward]
				arcvh
				flat (MosaicMiddle + cr) (SymbolMid + squareRadiusFW) [heading Leftward]
				curl (MosaicMiddle - 1) (SymbolMid + squareRadiusFW) [heading Leftward]

		define [ThickRoundedArrowBar x1 y1 x2 y2 r w] : glyph-proc
			include : PointingTo x1 y1 x2 y2 : lambda [mag] : spiro-outline
				g4 (-r) 0
				arcvh
				flat 0 r
				curl mag r
				archv
				g4 (mag + r) 0
				arcvh
				flat mag (-r)
				curl 0 (-r)
				archv
				close
		create-glyph [MangleName "whiteHeavySaltireWithRoundedCorners"] [MangleUnicode 0x1FBC0 WideWidth2] : glyph-proc
			set-width MosaicWidth
			local inner : [Math.max [adviceBlackness 3] (Width / 8)] / 2
			local thick : adviceBlackness 4
			local left : leftFW + (inner + thick)
			local right : rightFW - (inner + thick)
			local bot : bottomFW + (inner + thick)
			local top : topFW - (inner + thick)
			include : difference
				union
					ThickRoundedArrowBar left top right bot (inner + thick) 0
					ThickRoundedArrowBar right top left bot (inner + thick) 0
				union
					ThickRoundedArrowBar left top right bot inner 0
					ThickRoundedArrowBar right top left bot inner 0

		# Shaded hollow squares
		define [HShadeShape] : begin
			define lineWidth : Math.min GeometryStroke ((squareRadiusFW * 2 - GeometryStroke * 2) * 1 / 11)
			define top : SymbolMid + squareRadiusFW - GeometryStroke + lineWidth / 2
			define bot : SymbolMid - (top - SymbolMid)
			return : union
				dispiro
					widths.center lineWidth
					flat leftFW [mix top bot 0.25] [heading Rightward]
					curl rightFW [mix top bot 0.25] [heading Rightward]
				dispiro
					widths.center lineWidth
					flat leftFW [mix top bot 0.5] [heading Rightward]
					curl rightFW [mix top bot 0.5] [heading Rightward]
				dispiro
					widths.center lineWidth
					flat leftFW [mix top bot 0.75] [heading Rightward]
					curl rightFW [mix top bot 0.75] [heading Rightward]

		create-glyph [MangleName 'hShadeSquare'] [MangleUnicode 0x25A4] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'whiteSquare'
				intersection
					refer-glyph : MangleName 'blackSquare'
					HShadeShape

		define [VShadeShape] : begin
			define lineWidth : Math.min GeometryStroke ((squareRadiusFW * 2 - GeometryStroke * 2) * 1 / 11)
			define r : MosaicWidth / 2 + squareRadiusFW - GeometryStroke + lineWidth / 2
			define l : MosaicWidth / 2 - (r - MosaicWidth / 2)
			return : union
				dispiro
					widths.center lineWidth
					flat [mix l r 0.25] topFW [heading Downward]
					curl [mix l r 0.25] bottomFW [heading Downward]
				dispiro
					widths.center lineWidth
					flat [mix l r 0.5] topFW [heading Downward]
					curl [mix l r 0.5] bottomFW [heading Downward]
				dispiro
					widths.center lineWidth
					flat [mix l r 0.75] topFW [heading Downward]
					curl [mix l r 0.75] bottomFW [heading Downward]

		define [VBarShadeShape] : begin
			define lineWidth : Math.min GeometryStroke ((squareRadiusFW * 2 - GeometryStroke * 2) * 1 / 11)
			define r : MosaicWidth / 2 + squareRadiusFW - GeometryStroke + lineWidth / 2
			define l : MosaicWidth / 2 - (r - MosaicWidth / 2)
			return : union
				dispiro
					widths.center lineWidth
					flat [mix l r 0.5] topFW [heading Downward]
					curl [mix l r 0.5] bottomFW [heading Downward]

		create-glyph [MangleName 'vShadeSquare'] [MangleUnicode 0x25A5] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'whiteSquare'
				intersection
					refer-glyph : MangleName 'blackSquare'
					VShadeShape

		create-glyph [MangleName 'vBarSquare'] [MangleUnicode 0x25EB] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'whiteSquare'
				intersection
					refer-glyph : MangleName 'blackSquare'
					VBarShadeShape

		create-glyph [MangleName 'hvShadeSquare'] [MangleUnicode 0x25A6] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'hShadeSquare'
				refer-glyph : MangleName 'vShadeSquare'

		define [DDiagShadeShape] : begin
			define lineWidth : Math.min GeometryStroke ((squareRadiusFW * 2 - GeometryStroke * 2) * 1 / 11)
			define top : SymbolMid + squareRadiusFW
			define bot : SymbolMid - (top - SymbolMid)
			return : glyph-proc
				foreach [j : range (-10) till 10] : include : dispiro
					widths.center lineWidth
					flat leftFW [mix top bot (j / 4)] [heading Rightward]
					curl rightFW [mix top bot (j / 4 + 1)] [heading Rightward]

		define [UDiagShadeShape] : begin
			define lineWidth : Math.min GeometryStroke ((squareRadiusFW * 2 - GeometryStroke * 2) * 1 / 11)
			define top : SymbolMid + squareRadiusFW
			define bot : SymbolMid - (top - SymbolMid)
			return : glyph-proc
				foreach [j : range (-10) till 10] : include : dispiro
					widths.center lineWidth
					flat leftFW [mix top bot (j / 4)] [heading Rightward]
					curl rightFW [mix top bot (j / 4 - 1)] [heading Rightward]

		create-glyph [MangleName 'dDiagShadeSquare'] [MangleUnicode 0x25A7] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'whiteSquare'
				intersection
					refer-glyph : MangleName 'blackSquare'
					DDiagShadeShape

		create-glyph [MangleName 'uDiagShadeSquare'] [MangleUnicode 0x25A8] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'whiteSquare'
				intersection
					refer-glyph : MangleName 'blackSquare'
					UDiagShadeShape

		create-glyph [MangleName 'udDiagShadeSquare'] [MangleUnicode 0x25A9] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'dDiagShadeSquare'
				refer-glyph : MangleName 'uDiagShadeSquare'

		define [CircleMarkShape] : begin
			define r : squareRadiusFW - GeometryStroke / 2
			return : spiro-outline
				g4 MosaicMiddle (SymbolMid + r)
				g4 (MosaicMiddle - r) SymbolMid
				g4 MosaicMiddle (SymbolMid - r)
				g4 (MosaicMiddle + r) SymbolMid
				close
		create-glyph [MangleName 'vShadeCircle'] [MangleUnicode 0x25CD] : glyph-proc
			set-width MosaicWidth
			include : union
				refer-glyph : MangleName 'whiteCircle'
				intersection [CircleMarkShape] [VShadeShape]

		# partial circles
		define partialCircleQuarters : list
			list 0x25D0 1 1 0 0
			list 0x25D1 0 0 1 1
			list 0x25D2 0 1 0 1
			list 0x25D3 1 0 1 0
			list 0x25D4 0 0 1 0
			list 0x25D5 0 1 1 1
		foreach [{u LT LB RT RB} : items-of partialCircleQuarters] : begin
			create-glyph [MangleName ('uni' + u)] [MangleUnicode u] : glyph-proc
				set-width MosaicWidth
				include : union
					refer-glyph : MangleName 'whiteCircle'
					intersection
						CircleMarkShape
						new-glyph : glyph-proc
							if LT : include : UpperLeftQuarterMark
							if LB : include : LowerLeftQuarterMark
							if RT : include : UpperRightQuarterMark
							if RB : include : LowerRightQuarterMark

		define arcQuarters : list
			list 0x25DC 1 0 0 0
			list 0x25DD 0 0 1 0
			list 0x25DE 0 0 0 1
			list 0x25DF 0 1 0 0
			list 0x25E0 1 0 1 0
			list 0x25E1 0 1 0 1
		foreach [{u LT LB RT RB} : items-of arcQuarters] : begin
			create-glyph [MangleName ('uni' + u)] [MangleUnicode u] : glyph-proc
				set-width MosaicWidth
				include : intersection
					refer-glyph : MangleName 'whiteCircle'
					new-glyph : glyph-proc
						if LT : include : UpperLeftQuarterMark
						if LB : include : LowerLeftQuarterMark
						if RT : include : UpperRightQuarterMark
						if RB : include : LowerRightQuarterMark

		# Partial square
		define squareParts : list
			list 0x25E7 1 1 0 0 0 0 0 0
			list 0x25E8 0 0 1 1 0 0 0 0
			list 0x25E9 0 0 0 0 1 0 0 0
			list 0x2B14 0 0 0 0 0 0 1 0
			list 0x2B15 0 0 0 0 0 1 0 0
			list 0x25EA 0 0 0 0 0 0 0 1
		foreach [{u LT LB RT RB LTh LBh RTh RBh} : items-of squareParts] : begin
			create-glyph [MangleName ('uni' + u)] [MangleUnicode u] : glyph-proc
				set-width MosaicWidth
				include : union
					refer-glyph : MangleName 'whiteSquare'
					intersection
						refer-glyph : MangleName 'blackSquare'
						new-glyph : glyph-proc
							if LT : include : UpperLeftQuarterMark
							if LB : include : LowerLeftQuarterMark
							if RT : include : UpperRightQuarterMark
							if RB : include : LowerRightQuarterMark
							if LTh : include : UpperLeftHalfMark
							if LBh : include : LowerLeftHalfMark
							if RTh : include : UpperRightHalfMark
							if RBh : include : LowerRightHalfMark

		# Partial diamond
		define diamondParts : list
			list 0x2B16 1 1 0 0
			list 0x2B17 0 0 1 1
			list 0x2B18 1 0 1 0
			list 0x2B19 0 1 0 1
		foreach [{u LT LB RT RB LTh LBh RTh RBh} : items-of diamondParts] : begin
			create-glyph [MangleName ('uni' + u)] [MangleUnicode u] : glyph-proc
				set-width MosaicWidth
				include : union
					refer-glyph : MangleName 'whiteDiamond'
					intersection
						refer-glyph : MangleName 'blackDiamond'
						new-glyph : glyph-proc
							if LT : include : UpperLeftQuarterMark
							if LB : include : LowerLeftQuarterMark
							if RT : include : UpperRightQuarterMark
							if RB : include : LowerRightQuarterMark

		# Partial triangle
		define triangleParts : list
			list 0x25ED 1 1 0 0
			list 0x25EE 0 0 1 1
		foreach [{u LT LB RT RB LTh RBh} : items-of triangleParts] : begin
			create-glyph [MangleName ('uni' + u)] [MangleUnicode u] : glyph-proc
				set-width MosaicWidth
				include : union
					refer-glyph : MangleName 'whiteTriangleUp'
					intersection
						refer-glyph : MangleName 'blackTriangleUp'
						new-glyph : glyph-proc
							if LT : include : UpperLeftQuarterMark
							if LB : include : LowerLeftQuarterMark
							if RT : include : UpperRightQuarterMark
							if RB : include : LowerRightQuarterMark
							if LTh : include : UpperLeftHalfMark
							if RBh : include : LowerRightHalfMark

		# Quarter-line square and circle
		define quarterLineParts : list
			list 0x25F0 'whiteSquare' 1 1 0 0
			list 0x25F1 'whiteSquare' 0 1 1 0
			list 0x25F2 'whiteSquare' 0 0 1 1
			list 0x25F3 'whiteSquare' 1 0 0 1
			list 0x25F4 'whiteCircle' 1 1 0 0
			list 0x25F5 'whiteCircle' 0 1 1 0
			list 0x25F6 'whiteCircle' 0 0 1 1
			list 0x25F7 'whiteCircle' 1 0 0 1
		foreach { u frame T L B R } [items-of quarterLineParts] : begin
			create-glyph [MangleName ('uni' + u)] [MangleUnicode u] : glyph-proc
				local hh : GeometryStroke / 2 * HVContrast
				local hv : GeometryStroke / 2
				local s    GeometryStroke
				set-width MosaicWidth
				include : union
					refer-glyph [MangleName frame]
					if L [HBar (MosaicMiddle - squareRadiusFW + hh) (MosaicMiddle + hh) SymbolMid s] [glyph-proc]
					if R [HBar (MosaicMiddle + squareRadiusFW - hh) (MosaicMiddle - hh) SymbolMid s] [glyph-proc]
					if T [VBar MosaicMiddle (SymbolMid + squareRadiusFW - hv) (SymbolMid - hv) s]    [glyph-proc]
					if B [VBar MosaicMiddle (SymbolMid - squareRadiusFW + hv) (SymbolMid + hv) s]    [glyph-proc]

		# White diagonal-half square
		define whiteHalfTriangleParts : list
			list 0x25F8 1 0 0 0 1 0
			list 0x25F9 0 1 0 0 0 1
			list 0x25FA 0 0 1 0 0 1
			list 0x25FF 0 0 0 1 1 0
		foreach { u LT RT LB RB P Q } [items-of whiteHalfTriangleParts] : begin
			create-glyph [MangleName ('uni' + u)] [MangleUnicode u] : glyph-proc
				set-width MosaicWidth
				include : intersection
					intersection
						refer-glyph [MangleName 'blackSquare']
						union
							if LT [UpperLeftHalfMark]  [glyph-proc]
							if RT [UpperRightHalfMark] [glyph-proc]
							if LB [LowerLeftHalfMark]  [glyph-proc]
							if RB [LowerRightHalfMark] [glyph-proc]
					union
						refer-glyph [MangleName 'whiteSquare']
						if [not P] [glyph-proc] : dispiro
							widths.center (GeometryStroke * 2)
							corner (MosaicMiddle - squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)
							corner (MosaicMiddle + squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)
						if [not Q] [glyph-proc] : dispiro
							widths.center (GeometryStroke * 2)
							corner (MosaicMiddle + squareRadiusFW * 2) (SymbolMid - squareRadiusFW * 2)
							corner (MosaicMiddle - squareRadiusFW * 2) (SymbolMid + squareRadiusFW * 2)

		create-glyph [MangleName 'dottedWhiteTriangle'] [MangleUnicode 0x25EC] : glyph-proc
			set-width MosaicWidth
			include : refer-glyph : MangleName 'whiteTriangleUp'
			include : CircleDotAt MosaicMiddle (SymbolMid - squareRadiusFW / 3) [Math.max (Width / 6) GeometryStroke]

		create-glyph [MangleName 'uni25D8'] [MangleUnicode 0x25D8] : glyph-proc
			set-width MosaicWidth
			include : difference
				refer-glyph : MangleName 'blackLongRectangle'
				refer-glyph : MangleName 'blackCircle'

		create-glyph [MangleName 'uni25D9'] [MangleUnicode 0x25D9] : glyph-proc
			set-width MosaicWidth
			include : difference
				refer-glyph : MangleName 'blackLongRectangle'
				refer-glyph : MangleName 'whiteCircle'

		create-glyph [MangleName 'uni25DA'] [MangleUnicode 0x25DA] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'uni25D9'
				UpperHalfMask

		create-glyph [MangleName 'uni25DB'] [MangleUnicode 0x25DB] : glyph-proc
			set-width MosaicWidth
			include : intersection
				refer-glyph : MangleName 'uni25D9'
				LowerHalfMask

		create-glyph : glyph-proc
			set-width MosaicWidth
			local dotRadius : squareRadiusFW * 0.44
			local rayInnerRadius : squareRadiusFW * 0.56
			local rayWidth : adviceBlackness 8 MosaicWidthScalar
			foreach [n : range 0 8] : begin
				local theta : n / 8 * Math.PI * 2
				local c : Math.cos theta
				local s : Math.sin theta
				include : dispiro
					widths.center rayWidth
					corner (MosaicMiddle + c * rayInnerRadius) (SymbolMid + s * rayInnerRadius)
					corner (MosaicMiddle + c * squareRadiusFW) (SymbolMid + s * squareRadiusFW)

			create-derived [MangleName 'blackSunWithRays'] [MangleUnicode 0x2600]
				RingAt MosaicMiddle SymbolMid dotRadius
			create-derived [MangleName 'whiteSunWithRays'] [MangleUnicode 0x263C]
				RingStrokeAt MosaicMiddle SymbolMid dotRadius rayWidth
